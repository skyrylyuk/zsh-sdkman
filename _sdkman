#compdef sdk
#autoload

# sdk zsh completion, based on homebrew completion

local -a _1st_arguments
_1st_arguments=(
  'install:install a candidate version'
  'uninstall:uninstall a candidate version'
  'list:list available candidate versions'
  'use:use a candidate version in current shell'
  'default:set the default candidate version for every shell'
  'current:display current candidate version'
  'outdated:display outdated candidate version'
  'version:display the current version of sdkman'
  'broadcast:display the last broadcast message'
  'help:show the sdkman help message'
  'offline:enable or disable offline mode'
  'selfupdate:update the sdkman'
  'flush:flush local data'
)

local -a _2nd_arguments
_2nd_arguments=(
  'asciidoctorj:java bindings for asciidoctor'
  'crash:the shell for the java platform'
  'gaiden:create documentation with markdown easily'
  'glide:create awesome apps on google app engine'
  'gradle:build tool using groovy dsl'
  'grails:full-stack web framework'
  'griffon:framework for building desktop application'
  'groovy:dynamic jvm programming language'
  'groovyserv:run groovy as a server, reduces start-up time'
  'jbake:static site/blog generator for developers & designers'
  'lazybones:project creation tool that uses packaged project templates'
  'springboot:create a spring application with minimum setup'
  'vertx:event driven application framework'
)

local -a _toggle_mode
_toggle_mode=(
  'enable:enable offline mode'
  'disable:disable offline mode'
)

local -a _flush_candidates
_flush_candidates=(
   'candidates:flush product candidates'
   'broadcast:flush broadcast message'
   'archives:flush local cache files of archives'
   'temp:flush temporary data'
)

local expl

_arguments \
  '*:: :->subcmds' && return 0

if (( CURRENT == 1 )); then
  _describe -t commands "sdk subcommand" _1st_arguments
  return
fi

case "$words[1]" in
  install|uninstall|list|use|current|outdated|default)
    _describe -t commands "sdk subcommand" _2nd_arguments ;;
  i|rm|ls|u|c|d)
    _describe -t commands "sdk subcommand" _2nd_arguments ;;
  offline)
    _describe -t commands "sdk subcommand" _toggle_mode ;;
  flush)
    _describe -t commands "sdk subcommand" _flush_candidates ;;
esac
